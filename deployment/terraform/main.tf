resource "azurerm_resource_group" "cluster" {
  name     = var.groupe
  location = var.region
  tags     = var.marqueurs
}

resource "azurerm_virtual_network" "cluster" {
  name                = "reseau-solarhub"
  address_space       = ["10.0.0.0/16"]
  location            = azurerm_resource_group.cluster.location
  resource_group_name = azurerm_resource_group.cluster.name
  tags                = var.marqueurs
}

resource "azurerm_subnet" "noeuds" {
  name                 = "noeuds"
  resource_group_name  = azurerm_resource_group.cluster.name
  virtual_network_name = azurerm_virtual_network.cluster.name
  address_prefixes     = ["10.0.1.0/24"]
}

resource "azurerm_network_security_group" "cluster" {
  name                = "securite-solarhub"
  location            = azurerm_resource_group.cluster.location
  resource_group_name = azurerm_resource_group.cluster.name
  tags                = var.marqueurs

  security_rule {
    name                       = "ssh-administration"
    priority                   = 100
    direction                  = "Inbound"
    access                     = "Allow"
    protocol                   = "Tcp"
    source_port_range          = "*"
    destination_port_range     = "22"
    source_address_prefix      = var.adresse_administration
    destination_address_prefix = "*"
  }

  security_rule {
    name                       = "api-kubernetes"
    priority                   = 105
    direction                  = "Inbound"
    access                     = "Allow"
    protocol                   = "Tcp"
    source_port_range          = "*"
    destination_port_range     = "6443"
    source_address_prefix      = var.adresse_administration
    destination_address_prefix = "*"
  }

  security_rule {
    name                       = "http-public"
    priority                   = 110
    direction                  = "Inbound"
    access                     = "Allow"
    protocol                   = "Tcp"
    source_port_range          = "*"
    destination_port_range     = "80"
    source_address_prefix      = "Internet"
    destination_address_prefix = "*"
  }

  security_rule {
    name                       = "https-public"
    priority                   = 120
    direction                  = "Inbound"
    access                     = "Allow"
    protocol                   = "Tcp"
    source_port_range          = "*"
    destination_port_range     = "443"
    source_address_prefix      = "Internet"
    destination_address_prefix = "*"
  }
}

resource "azurerm_subnet_network_security_group_association" "cluster" {
  subnet_id                 = azurerm_subnet.noeuds.id
  network_security_group_id = azurerm_network_security_group.cluster.id
}

locals {
  noeuds = {
    controle = { taille = var.taille_controle, role = "controle",   adresse = "10.0.1.10" }
    charge   = { taille = var.taille_charge,   role = "applicatif", adresse = "10.0.1.11" }
  }
}

resource "azurerm_public_ip" "noeud" {
  for_each            = local.noeuds
  name                = "ip-${each.key}"
  location            = azurerm_resource_group.cluster.location
  resource_group_name = azurerm_resource_group.cluster.name
  allocation_method   = "Static"
  sku                 = "Standard"
  tags                = var.marqueurs
}

resource "azurerm_network_interface" "noeud" {
  for_each            = local.noeuds
  name                = "carte-${each.key}"
  location            = azurerm_resource_group.cluster.location
  resource_group_name = azurerm_resource_group.cluster.name
  tags                = var.marqueurs

  ip_configuration {
    name                          = "interne"
    subnet_id                     = azurerm_subnet.noeuds.id
    private_ip_address_allocation = "Static"
    private_ip_address            = each.value.adresse
    public_ip_address_id          = azurerm_public_ip.noeud[each.key].id
  }
}

resource "azurerm_linux_virtual_machine" "noeud" {
  for_each            = local.noeuds
  name                = "vm-${each.key}"
  resource_group_name = azurerm_resource_group.cluster.name
  location            = azurerm_resource_group.cluster.location
  size                = each.value.taille
  admin_username      = var.utilisateur

  network_interface_ids = [azurerm_network_interface.noeud[each.key].id]

  disable_password_authentication = true

  admin_ssh_key {
    username   = var.utilisateur
    public_key = file(pathexpand(var.chemin_cle_publique))
  }

  os_disk {
    caching              = "ReadWrite"
    storage_account_type = "StandardSSD_LRS"
    disk_size_gb         = each.value.role == "applicatif" ? 64 : 32
  }

  source_image_reference {
    publisher = "Canonical"
    offer     = "ubuntu-24_04-lts"
    sku       = "server"
    version   = "latest"
  }

  tags = merge(var.marqueurs, { role = each.value.role })
}
